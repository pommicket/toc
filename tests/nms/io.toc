get_type_with_size ::= fn(size :: i64) Type {
	if size == 1 { i8 }
	elif size == 2 { i16 }
	elif size == 4 { i32 }
	elif size == 8 { i64 }
	else { f32 }
};

get_utype_with_size ::= fn(size :: i64) Type {
	if size == 1 { u8 }
	elif size == 2 { u16 }
	elif size == 4 { u32 }
	elif size == 8 { u64 }
	else { f32 }
};

c_int ::= get_type_with_size(#builtin("sizeof int"));
c_size_t ::= get_utype_with_size(#builtin("sizeof size_t"));

c_putchar ::= #foreign("putchar", "libc.so.6") fn(#C int) #C int;
toc_putchar ::= fn(x: char) {
		c_putchar(x as c_int);
};

c_fwrite ::= #foreign("fwrite", "libc.so.6") fn(&u8, #C size_t, #C size_t, &u8) #C size_t;

stdout_fwrite ::= fn(data: &u8, size: u64, nmemb: u64) {
	c_fwrite(data, size as c_size_t, nmemb as c_size_t, #builtin("stdout"));
};

puts ::= fn(x: []char) {
	stdout_fwrite(&x[0] as &u8, 1, x.len as u64);
	toc_putchar('\n');
};

puti ::= fn(x: int) {
	if x < 0 {
	   toc_putchar('-');
	   // NOTE: don't do x = -x; here to make sure I64_MIN works
	}
	if x == 0 {
		toc_putchar('0');
	} else {
		abs ::= fn(x: int) int { if x < 0 { -x } else { x } };
		scan_digit := 1000000000000000000;
		started := false;
		while scan_digit > 0 {
			digit := abs((x / scan_digit) % 10);
			if digit > 0 { started = true; }
			if started {
			   toc_putchar((('0' as int) + digit) as char);
			}
			scan_digit /= 10;
		}
	}
	toc_putchar('\n');
};
